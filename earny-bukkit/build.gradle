plugins {
    id 'java'
    id 'eclipse'
    id "io.franzbecker.gradle-lombok" version "3.1.0"
    id "org.zeroturnaround.gradle.jrebel" version '1.1.3'
    id "com.github.johnrengelman.shadow" version "5.1.0"
}
String packagePath = 'de.deeprobin.earny'
group packagePath
version '1.0.0'

sourceCompatibility = 1.8

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

artifacts {
    archives shadowJar
}

repositories {
    mavenCentral()
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'

        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }

    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://repo.codemc.org/repository/maven-public' }
    maven { url "http://repo.dmulloy2.net/nexus/repository/public/" }
}

dependencies {
    compile project(":earny-core")
    compile 'org.bstats:bstats-bukkit:1.5'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compileOnly 'org.spigotmc:spigot-api:1.14.3-R0.1-SNAPSHOT'
    compileOnly group: 'net.md-5', name: 'bungeecord-api', version: '1.14-SNAPSHOT'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Earny',
                'Implementation-Version': version
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

shadowJar {
    relocate('org.bstats.bukkit', packagePath + '.lib.bstats')

    version = version
}

processResources {
    inputs.property "version", project.version
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'
        expand 'version': project.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'plugin.yml'
    }
}

lombok {
    version = "1.18.8"
    sha256 = "0396952823579b316a0fe85cbd871bbb3508143c2bcbd985dd7800e806cb24fc"
}