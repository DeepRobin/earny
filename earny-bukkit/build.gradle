plugins {
    id 'java'
    id 'eclipse'
    id "io.franzbecker.gradle-lombok"
    id "org.zeroturnaround.gradle.jrebel"
    id "com.github.johnrengelman.shadow"
}
String packagePath = 'de.deeprobin.earny.platform.bukkit'
group packagePath
version ver

sourceCompatibility = 1.8

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

artifacts {
    archives shadowJar
}

repositories {
    mavenCentral()
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'

        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }

    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://repo.codemc.org/repository/maven-public/' }
    maven { url "http://repo.dmulloy2.net/nexus/repository/public/" }
}

dependencies {
    compileOnly project(":earny-core")
    compileOnly 'org.spigotmc:spigot-api:1.14.3-R0.1-SNAPSHOT' // todo: use raw bukkit
    compileOnly group: 'net.md-5', name: 'bungeecord-api', version: '1.14-SNAPSHOT'

    compile 'org.bstats:bstats-bukkit:1.5'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Earny',
                'Implementation-Version': version
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

shadowJar {
    relocate('org.bstats.bukkit', packagePath + '.lib.bstats')

    version = version
}

processResources {
    inputs.property "version", project.version
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'
        expand 'version': project.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'plugin.yml'
    }
}

lombok {
    version = "1.18.8"
    sha256 = "0396952823579b316a0fe85cbd871bbb3508143c2bcbd985dd7800e806cb24fc"
}